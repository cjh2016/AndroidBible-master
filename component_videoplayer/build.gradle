if (isRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion build_versions.target_sdk

    defaultConfig {
        if (isRunAlone.toBoolean()) {
            applicationId "com.cjh.component_videoplayer"
        }

        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.version_code
        versionName build_versions.version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        if (isRunAlone.toBoolean()) {
            multiDexEnabled true
        }
    }

    sourceSets {
        main {
            if (isRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }

            jniLibs.srcDirs = [ 'src/main/jniLibs' ]
        }
    }

    //支持java1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api project(':lib_common')


    kapt deps.arouter.compiler
    api 'com.google.android.exoplayer:exoplayer-core:2.9.1'
    api 'com.google.android.exoplayer:exoplayer-dash:2.9.1'
    api 'com.google.android.exoplayer:exoplayer-hls:2.9.1'
    api 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.9.1'
    api 'tv.danmaku.ijk.media:ijkplayer-java:0.8.8'
    implementation files('libs/logtools.jar')


    implementation deps.multidex.multidex

    annotationProcessor deps.glide.compiler


}
